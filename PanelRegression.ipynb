{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {
    "deletable": true,
    "editable": true
   },
   "source": [
    "# Calendar Time Regresasions vs. Panel Regressions"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {
    "collapsed": false,
    "deletable": true,
    "editable": true
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "OlsFn (generic function with 1 method)"
      ]
     },
     "execution_count": 1,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "include(\"jlFiles/printmat.jl\")\n",
    "include(\"jlFiles/OlsFn.jl\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {
    "collapsed": false,
    "deletable": true,
    "editable": true
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "done loading data\n"
     ]
    }
   ],
   "source": [
    "ER1 = readdlm(\"Data/PPM_ER1.csv\",',')                   #load data from csv files\n",
    "ER2 = readdlm(\"Data/PPM_ER2.csv\",',')\n",
    "ER  = [ER1;ER2]\n",
    "#ER = randn(2354,2637)           #uncomment this line (and comment out the previous 3 lines)\n",
    "                                 #if you do not have ER1.csv and ER2.csv\n",
    "(ER1,ER2) = (nothing,nothing)\n",
    "\n",
    "Factors   = readdlm(\"Data/PPM_Factors.csv\",',')         #no header line: x is matrix\n",
    "Investors = readdlm(\"Data/PPM_N_Changes.csv\",',')\n",
    "N_Changes = Investors[:,1]\n",
    "\n",
    "(T,N) = size(ER)\n",
    "D     = N_Changes .> 50                #logical dummies: [active]\n",
    "D0    = broadcast(!,D)                 #works on both 0.5 and 0.6. Could do .!D on 0.6\n",
    "\n",
    "println(\"done loading data\")"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {
    "deletable": true,
    "editable": true
   },
   "source": [
    "## Individual alphas"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {
    "deletable": true,
    "editable": true
   },
   "source": [
    "The following code takes the matrix of individual daily\n",
    "excess return $ER_{T\\times N}$ and runs one regression for each individual on\n",
    "a three risk $Factors_{T\\times3}$ (excess returns on Swedish equity, Swedish\n",
    "bonds and international equity). \n",
    "\n",
    "The $D_{N\\times1}$ vector contains dummies: 0 (false) if inactive investor, 1 (true) if active investor. \n",
    "\n",
    "The code shows the average alphas for the two groups."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {
    "collapsed": false,
    "deletable": true,
    "editable": true
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\n",
      "Average annualised alphas for the two groups\n",
      "    -0.787     6.217\n",
      "\n"
     ]
    }
   ],
   "source": [
    "alphaM = fill(NaN,N)                                #individual alphas\n",
    "for i = 1:N\n",
    "   b, = OlsFn(ER[:,i],[Factors ones(T)])\n",
    "   alphaM[i] = b[end]\n",
    "end\n",
    "\n",
    "println(\"\\nAverage annualised alphas for the two groups\")\n",
    "printmat([mean(alphaM[D0]) mean(alphaM[D])]*252)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {
    "deletable": true,
    "editable": true
   },
   "source": [
    "## Calendar Time Portfolios"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {
    "deletable": true,
    "editable": true
   },
   "source": [
    "The following code creates two time series\n",
    "$T\\times1$ of portfolio returns: one for the cross-sectional average return\n",
    "of inactive investor, another for active investors. \n",
    "\n",
    "Then, it calculates the average excess returns and the Sharpe ratios. \n",
    "\n",
    "The alphas and betas are estimated with OLS, and we test the hypothesis that the two alphas are the\n",
    "same (using a SURE approach)."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "metadata": {
    "collapsed": false,
    "deletable": true,
    "editable": true
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\n",
      "LS, group by group\n",
      "Stats for the portfolios:\n",
      " Avg, Std, SR, alpha\n",
      "    -1.262    15.728    -0.080    -0.787\n",
      "     5.534    13.882     0.399     6.217\n",
      "\n",
      "diff of annual alpha (inactive - 51+), tstat (LS)\n",
      "    -7.004    -2.780\n",
      "\n"
     ]
    }
   ],
   "source": [
    "println(\"\\nLS, group by group\")\n",
    "\n",
    "PortfER      = fill(NaN,(T,2))     #create portfolios as average across individuals\n",
    "PortfER[:,1] = mean(ER[:,D0],2)    #Tx1, portfolio return = average individual return\n",
    "PortfER[:,2] = mean(ER[:,D],2)\n",
    "\n",
    "Avg = mean(PortfER,1)*252          #average excess return on portfolios\n",
    "Std = std(PortfER,1)*sqrt(252)\n",
    "SR  = Avg./Std\n",
    "(b,res,yhat,Covb) = OlsFn(PortfER,[ones(T) Factors])\n",
    "\n",
    "println(\"Stats for the portfolios:\\n Avg, Std, SR, alpha\")\n",
    "printmat([Avg' Std' SR' b[1:1,:]'*252])\n",
    "\n",
    "R       = [1 0 0 0 -1 0 0 0]                       #testing if alpha(1) = alpha(2)\n",
    "a_diff  = R*vec(b)                                 \n",
    "tstatLS = a_diff/sqrt((R*Covb*R')[1])\n",
    "\n",
    "println(\"diff of annual alpha (inactive - 51+), tstat (LS)\")\n",
    "printmat([a_diff*252 tstatLS])"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {
    "deletable": true,
    "editable": true
   },
   "source": [
    "## Panel Regressions"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {
    "deletable": true,
    "editable": true
   },
   "source": [
    "Finally, a panel ($T\\times N$) regression is done by simply stacking all data\n",
    "points---but by interacting the factors (and constant) with the dummies. The\n",
    "hypothesis of the same alphas is tested by both an OLS approach (assuming that\n",
    "all data is iid) and a DK approach (which accounts for cross-sectional correlations).\n",
    "\n",
    "The code for that panel regression is in the function HszDkFn. It does a\n",
    "straightforward LS regression (by a loop over $t$, to save memory space) and\n",
    "then estimates the covariance matrix of the moment conditions as in\n",
    "DK (allowing for cross-sectional correlations)."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "metadata": {
    "collapsed": false,
    "deletable": true,
    "editable": true
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "HszDkFn (generic function with 1 method)"
      ]
     },
     "execution_count": 5,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function HDirProdFn(x,y)\n",
    "#HDirProdFn    Calculates horizontal direct product of two matrices with equal number of rows.\n",
    "#              z[i,:] is the Kronecker product of x[i,:] and y[i,:]\n",
    "  Kx = size(x,2)       #columns in x\n",
    "  Ky = size(y,2)       #columns in y\n",
    "  z = repmat(y,1,Kx) .* kron(x,ones(Int,1,Ky))\n",
    "  return z\n",
    "end\n",
    "\n",
    "function HszDkFn(y,x,z)\n",
    "#HszDkFn   LS and Driscoll-Kray standard errors for panel, assuming x(t,i) = x(t) * z(i)\n",
    "\n",
    "  (T,N) = size(y,1,2)\n",
    "  K = size(x,2)*size(z,2)\n",
    "\n",
    "  Sxx = 0.0\n",
    "  Sxy = 0.0\n",
    "  for t = 1:T                          #OLS by looping over t\n",
    "    y_t  = y[t:t,:]'                     #dependent variable, Nx1\n",
    "    x0_t = repmat(x[t:t,:],N,1)          #factors, NxK, could simplify?\n",
    "    x_t  = HDirProdFn(z,x0_t)          #effective regressors, z is NxKz, x_t is NxK\n",
    "    Sxx  = Sxx + x_t'x_t/(T*N)         #building up Sxx and Sxy\n",
    "    Sxy  = Sxy + x_t'y_t/(T*N)\n",
    "  end\n",
    "\n",
    "  theta = Sxx\\Sxy\n",
    "\n",
    "  s2     = 0.0\n",
    "  omegaj = zeros(K,K)\n",
    "  for t = 1:T                          #Covariance matrix by looping over t\n",
    "    y_t  = y[t:t,:]'                     #create y_t and x_t (again)\n",
    "    x0_t = repmat(x[t:t,:],N,1)\n",
    "    x_t  = HDirProdFn(z,x0_t)\n",
    "    e_t  = y_t - x_t*theta             #residuals in t\n",
    "    h_t  = (x_t'e_t)'/N                #moment conditions in t (divided by N)\n",
    "    omegaj = omegaj + h_t'h_t          #building up covariance matrix\n",
    "    s2     = s2 + sum(e_t.^2)/N^2\n",
    "  end\n",
    "  Shat = omegaj/T^2                     #estimate of S\n",
    "  s2   = s2/T^2\n",
    "\n",
    "  zx_1  = inv(Sxx)\n",
    "  CovDK = zx_1 * Shat * zx_1'                     #covariance matrix, DK\n",
    "  stdDK = sqrt.(diag(CovDK))                      #standard errors, DK\n",
    "\n",
    "  CovLS = zx_1 * s2                               #covariance matrix, LS iid\n",
    "  stdLS = sqrt.(diag(CovLS))                      #standard errors, LS iid\n",
    "\n",
    "  return theta,CovDK,CovLS\n",
    "\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "metadata": {
    "collapsed": false,
    "deletable": true,
    "editable": true
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\n",
      "panel\n",
      "diff of annual alpha (inactive - 51+), tstat (LS), tstat (DK)\n",
      "    -7.004   -24.017    -2.784\n",
      "\n",
      "\n",
      "Compare with calendar regressions. Also notice the difference between the t-stats\n"
     ]
    }
   ],
   "source": [
    "println(\"\\npanel\")\n",
    "(theta,CovDK,CovLS) = HszDkFn(ER,[ones(T) Factors],[D0 D]+0.0)\n",
    "\n",
    "R       = [1 0 0 0 -1 0 0 0]                #testing if alpha(1) = alpha(2)\n",
    "a_diff  = R*theta\n",
    "tstatLS = a_diff/sqrt.(R*CovLS*R')\n",
    "tstatDK = a_diff/sqrt.(R*CovDK*R')\n",
    "\n",
    "println(\"diff of annual alpha (inactive - 51+), tstat (LS), tstat (DK)\")\n",
    "printmat([a_diff*252 tstatLS tstatDK])\n",
    "\n",
    "println(\"\\nCompare with calendar regressions. Also notice the difference between the t-stats\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": true,
    "deletable": true,
    "editable": true
   },
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "anaconda-cloud": {},
  "kernelspec": {
   "display_name": "Julia 0.5.1",
   "language": "julia",
   "name": "julia-0.5"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "0.5.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 1
}
