{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# GMM"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Load Packages"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "Inn"
      ]
     },
     "execution_count": 1,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "using Dates, DelimitedFiles, Statistics, LinearAlgebra, Optim, NLsolve\n",
    "\n",
    "include(\"jlFiles/printmat.jl\")\n",
    "include(\"jlFiles/printTable.jl\")\n",
    "include(\"jlFiles/NWFn.jl\")\n",
    "\n",
    "\"\"\"\n",
    "Create identity matrix (n rows, n columns). In Julia 1.2, use the built-in I(n) instead.\n",
    "\"\"\"\n",
    "Inn(n) = Matrix(1.0I,n,n)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# GMM I\n",
    "\n",
    "This section describes the basic (exactly identified) GMM, that is, when we have as many moment conditions as parameters. (In this case GMM is the same as the classical method of moments, MM.)\n",
    "\n",
    "The first few cells load functions and data. See further down for the estimation."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "388"
      ]
     },
     "execution_count": 2,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "x = readdlm(\"Data/FFmFactorsPs.csv\",',',skipstart=1)   #start on line 2, column 1\n",
    "x = x[:,2]         #excess market returns, in %\n",
    "\n",
    "T = size(x,1)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Traditional Estimation of Mean and Variance\n",
    "\n",
    "The next cell applies the traditional way of estimating the mean and the variance."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\u001b[34m\u001b[1mTraditional estimates:\u001b[22m\u001b[39m\n",
      "        coef       std\n",
      "μ      0.602     0.233\n",
      "σ²    21.142     1.518\n",
      "\n"
     ]
    }
   ],
   "source": [
    "μ  = mean(x)                      #same as setting A*gbar=0\n",
    "σ² = var(x,corrected=false)       #\"false\" to use 1/T formula\n",
    "\n",
    "par_a = [μ;σ²]\n",
    "\n",
    "printblue(\"Traditional estimates:\")\n",
    "xx = [par_a [sqrt((σ²/T));sqrt(2*σ²^2/T)]]\n",
    "colNames = [\"coef\",\"std\"]\n",
    "rowNames = [\"μ\",\"σ²\"]\n",
    "printTable(xx,colNames,rowNames)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## GMM Point Estimates and Distribution\n",
    "\n",
    "To estimate the mean and variance of $x_{t}$, use the following moment condition\n",
    "\n",
    "$\n",
    "\\frac{1}{T}\\sum\\nolimits_{t=1}^{T}g_{t}=0 \\: \\text{ where } \n",
    "$\n",
    "\n",
    "$\n",
    "g_{t}(\\mu,\\sigma^{2})=\\left[\n",
    "\\begin{array}\n",
    "[c]{l}\n",
    "x_{t}-\\mu\\\\\n",
    "(x_{t}-\\mu)^{2}-\\sigma^{2}\n",
    "\\end{array}\n",
    "\\right] .\n",
    "$\n",
    "\n",
    "The parameter values ($\\mu,\\sigma^2$) that make these moment conditions hold must be the same as from the traditional method. In other cases, we may not know the GMM estimates. Therefore, the code below solves for the parameters (and also double checks that they indeed are the same as before).\n",
    "\n",
    "The distribution of the estimates is\n",
    "\n",
    "$\n",
    "\\sqrt{T}(\\hat{\\mu}-\\mu_{0})\\overset{d}{\\rightarrow}N(0,V) \n",
    "\\: \\text{ where } \\: \n",
    "V = (D_{0}^{\\prime}S_{0}^{-1}D_{0})  ^{-1}\n",
    "$\n",
    "\n",
    "Clearly, $D_{0}=-\\textrm{I}$ and if data is iid then $S_{0}=\\text{Var}(g_{t})$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "Gmm2MomFn (generic function with 1 method)"
      ]
     },
     "execution_count": 4,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function Gmm2MomFn(par,x)\n",
    "    (μ,σ²) = (par[1],par[2])\n",
    "    g      = [(x .- μ) ((x .- μ).^2 .- σ²)]  #Tx2         \n",
    "    gbar   = vec(mean(g,dims=1))             #2-element vector\n",
    "    return g,gbar\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\u001b[34m\u001b[1mGMM estimates:\u001b[22m\u001b[39m\n",
      "     0.602\n",
      "    21.142\n",
      "\n",
      "\u001b[34m\u001b[1mChecking if mean of g_t = 0\u001b[22m\u001b[39m\n",
      "     0.000\n",
      "     0.000\n",
      "\n"
     ]
    }
   ],
   "source": [
    "Sol = nlsolve(p->Gmm2MomFn(p,x)[2],par_a)   #solve for the GMM estimates\n",
    "par_1 = Sol.zero                            \n",
    "\n",
    "printblue(\"GMM estimates:\")\n",
    "printmat(par_1)\n",
    "\n",
    "(g,gbar) = Gmm2MomFn(par_1,x)           #Tx2, moment conditions\n",
    "printblue(\"Checking if mean of g_t = 0\")\n",
    "printmat(gbar)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\u001b[34m\u001b[1mGMM estimates:\u001b[22m\u001b[39m\n",
      "        coef       std\n",
      "μ      0.602     0.244\n",
      "σ²    21.142     2.381\n",
      "\n",
      "\u001b[31m\u001b[1mCompare with the traditional estimates\u001b[22m\u001b[39m"
     ]
    }
   ],
   "source": [
    "D  = -Inn(2)                 #Jacobian, does not really matter here\n",
    "S  = NWFn(g,1)               #Newey-West with 1 lag\n",
    "V1 = inv(D'inv(S)*D)\n",
    "\n",
    "printblue(\"GMM estimates:\")\n",
    "xx = [par_1 sqrt.(diag(V1/T))]\n",
    "printTable(xx,colNames,rowNames)\n",
    "\n",
    "printstyled(\"Compare with the traditional estimates\",color=:red,bold=true)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {
    "collapsed": true
   },
   "source": [
    "# GMM II\n",
    "\n",
    "This section expands the GMM calculations by doing an overidentified case: more moment conditions than parameters.\n",
    "\n",
    "Warning: some of the variables (```g,S,etc```) are overwritten with new values."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## The Moment Conditions\n",
    "\n",
    "If $x_{t}$ is $N(\\mu,\\sigma^{2})$, then the following moment conditions should\n",
    "all be zero (in expectation)\n",
    "\n",
    "$\n",
    "g_{t}(\\mu,\\sigma^{2})=\\left[\n",
    "\\begin{array}\n",
    "[c]{l}\n",
    "x_{t}-\\mu\\\\\n",
    "(x_{t}-\\mu)^{2}-\\sigma^{2}\\\\\n",
    "(x_{t}-\\mu)^{3}\\\\\n",
    "(x_{t}-\\mu)^{4}-3\\sigma^{4}\n",
    "\\end{array}\n",
    "\\right]  .\n",
    "$\n",
    "\n",
    "The first moment condition defines the mean $\\mu$, the second defines the\n",
    "variance $\\sigma^{2}$, while the third and forth are the skewness and excess\n",
    "kurtosis respectively."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "DGmm4MomFn (generic function with 1 method)"
      ]
     },
     "execution_count": 7,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function Gmm4MomFn(par,x)\n",
    "  (μ,σ²) = (par[1],par[2])\n",
    "  g      = [(x .- μ) ((x .- μ).^2 .- σ²) ((x .- μ).^3) ((x .- μ).^4 .- 3*σ²^2)]    #Tx4\n",
    "  gbar   = vec(mean(g,dims=1))     #4-element vector\n",
    "  return g,gbar\n",
    "end\n",
    "\n",
    "function DGmm4MomFn(par,x)\n",
    "    (μ,σ²) = (par[1],par[2])\n",
    "    D  = [-1                  0    ;                #Jacobian of Gmm4MomFn\n",
    "          -2*mean(x.-μ)      -1    ;\n",
    "          -3*mean((x.-μ).^2)   0   ;\n",
    "          -4*mean((x.-μ).^3)  -6*σ²]\n",
    "    return D\n",
    "end    "
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## GMM: Minimizing g'W*g\n",
    "\n",
    "\n",
    "The following code applies a numerical method to solve a minimization problem with the weighting matrix \n",
    "\n",
    "$\n",
    "W=\n",
    "\\begin{bmatrix}\n",
    "1 & 0 & 0 & 0\\\\\n",
    "0 & 1 & 0 & 0\\\\\n",
    "0 & 0 & 0 & 0\\\\\n",
    "0 & 0 & 0 & 0\n",
    "\\end{bmatrix}\n",
    "$\n",
    "\n",
    "The results should be the same (or at least very close to) the previous results, since the $W$ matrix puts all weight on the first two moments (basically mimicking the estimations above). Changing $W$, for instance, by setting $W[3,3]=0.0001$ will give other estimates.\n",
    "\n",
    "We define the loss function as \n",
    "\n",
    "$\n",
    "\\bar{g}'W\\bar{g}\n",
    "$\n",
    "\n",
    "As a practical matter, it is often the case that a derivative-free method works better than other optimization routines."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 8,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "Gmm4MomLossFn (generic function with 2 methods)"
      ]
     },
     "execution_count": 8,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function Gmm4MomLossFn(par,x,W=1)\n",
    "  (g,gbar) = Gmm4MomFn(par,x)\n",
    "  Loss     = 1.0 + gbar'W*gbar      #to be minimized\n",
    "  return Loss\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 9,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\u001b[34m\u001b[1mGMM estimates:\u001b[22m\u001b[39m\n",
      "     0.602\n",
      "    21.142\n",
      "\n"
     ]
    }
   ],
   "source": [
    "W     = diagm(0=>[1.0,1.0,0.0,0.0])   #weighting matrix, try changing it\n",
    "#W[3,3] = 0.0001\n",
    "Sol   = optimize(par->Gmm4MomLossFn(par,x,W),par_a)\n",
    "par_2 = Optim.minimizer(Sol)\n",
    "\n",
    "printblue(\"GMM estimates:\")\n",
    "printmat(par_2)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 10,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\u001b[34m\u001b[1mThe Jacobian is:\u001b[22m\u001b[39m\n",
      "    -1.000     0.000\n",
      "    -0.000    -1.000\n",
      "   -63.427     0.000\n",
      "   314.797  -126.854\n",
      "\n",
      "\u001b[34m\u001b[1mWeighting matrix\u001b[22m\u001b[39m\n",
      "     1.000     0.000     0.000     0.000\n",
      "     0.000     1.000     0.000     0.000\n",
      "     0.000     0.000     0.000     0.000\n",
      "     0.000     0.000     0.000     0.000\n",
      "\n",
      "\u001b[34m\u001b[1mGMM estimates (g'W*g):\u001b[22m\u001b[39m\n",
      "        coef       std\n",
      "μ      0.602     0.244\n",
      "σ²    21.142     2.381\n",
      "\n"
     ]
    }
   ],
   "source": [
    "D  = DGmm4MomFn(par_2,x)               #Jacobian\n",
    "printblue(\"The Jacobian is:\")\n",
    "printmat(D)\n",
    "\n",
    "g,    = Gmm4MomFn(par_2,x)                #Tx4, moment conditions, evaluated at point estimates\n",
    "S     = NWFn(g,1)                         #variance of sqrt(T)\"gbar, NW with 1 lag\n",
    "V2    = inv(D'W*D)*D'W*S*W'D*inv(D'W*D)\n",
    "\n",
    "printblue(\"Weighting matrix\")\n",
    "printmat(W)\n",
    "\n",
    "printblue(\"GMM estimates (g'W*g):\")\n",
    "xx = [par_2 sqrt.(diag(V2/T))]\n",
    "printTable(xx,colNames,rowNames)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## GMM: A*g = 0\n",
    "\n",
    "\n",
    "The following code from estimates the parameters (mean and\n",
    "variance) by combining the 4 original moment conditions in $\\bar{g}$ into 2\n",
    "effective moment conditions, $A\\bar{g}$, where $A$ is a $2\\times4$ matrix\n",
    "\n",
    "$\n",
    "A=\\left[\n",
    "\\begin{array}\n",
    "[c]{cccc}\n",
    "1 & 0 & 0 & 0\\\\\n",
    "0 & 1 & 0 & 0\n",
    "\\end{array}\n",
    "\\right]  \n",
    "$ \n",
    "\n",
    "This particular $A$ matrix implies that we use the classical\n",
    "estimators of the mean and variance. Changing $A$,for instance, setting $A[1,3]=0.001$ will give different estimates."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 11,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\u001b[34m\u001b[1mGMM estimates:\u001b[22m\u001b[39m\n",
      "     0.602\n",
      "    21.142\n",
      "\n",
      "\n",
      "\u001b[34m\u001b[1mChecking if mean of A*g_t = 0\u001b[22m\u001b[39m\n",
      "     0.000\n",
      "     0.000\n",
      "\n"
     ]
    }
   ],
   "source": [
    "A = [1 0 0 0;                   #A in A*gbar=0 (here: all weight on first two moments)\n",
    "     0 1 0 0]                   #try setting A[1,3] = 0.001\n",
    "            \n",
    "Sol = nlsolve(p->A*Gmm4MomFn(p,x)[2],par_a)   #solve for the GMM estimates\n",
    "par_3 = Sol.zero\n",
    "\n",
    "printblue(\"GMM estimates:\")\n",
    "printmat(par_3)\n",
    "\n",
    "(g,gbar) = Gmm4MomFn(par_3,x)        #Tx4, moment conditions. Warning: overwriting old g\n",
    "q = size(g,2)\n",
    "\n",
    "printblue(\"\\nChecking if mean of A*g_t = 0\")\n",
    "printmat(A*gbar)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 12,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\u001b[34m\u001b[1mThe Jacobian is:\u001b[22m\u001b[39m\n",
      "    -1.000     0.000\n",
      "    -0.000    -1.000\n",
      "   -63.427     0.000\n",
      "   314.797  -126.854\n",
      "\n",
      "\u001b[34m\u001b[1mGMM estimates (A*gbar):\u001b[22m\u001b[39m\n",
      "        coef       std\n",
      "μ      0.602     0.244\n",
      "σ²    21.142     2.381\n",
      "\n",
      "\u001b[31m\u001b[1mCompare with the exactly identified GMM (above)\u001b[22m\u001b[39m"
     ]
    }
   ],
   "source": [
    "D  = DGmm4MomFn(par_3,x)               #Jacobian\n",
    "printblue(\"The Jacobian is:\")\n",
    "printmat(D)\n",
    "\n",
    "S  = NWFn(g,1)\n",
    "V3 = inv(A*D)*A*S*A'inv(A*D)'\n",
    "\n",
    "printblue(\"GMM estimates (A*gbar):\")\n",
    "xx = [par_3 sqrt.(diag(V3/T))]\n",
    "printTable(xx,colNames,rowNames)\n",
    "\n",
    "printstyled(\"Compare with the exactly identified GMM (above)\",color=:red,bold=true)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# GMM: Minimizing g'Wg, Iterating over W\n",
    "\n",
    "\n",
    "The following code iterates over the weighting matrix by using $W=S^{-1}$, where\n",
    "\n",
    "$S = \\text{Cov}(\\sqrt{T}\\bar{g})$ \n",
    "\n",
    "is from the previous iteration."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 13,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\n",
      "iterated GMM, using optimal weighting matrix, starting with S from previous estimation\n",
      "\n",
      "\n",
      "iterating over W starting with the W choice above\n",
      "-------iteration  1, old and new parameters--------\n",
      "     0.602    21.142\n",
      "     0.877    16.916\n",
      "-------iteration  2, old and new parameters--------\n",
      "     0.877    16.916\n",
      "     0.879    16.648\n",
      "-------iteration  3, old and new parameters--------\n",
      "     0.879    16.648\n",
      "     0.879    16.645\n",
      "-------iteration  4, old and new parameters--------\n",
      "     0.879    16.645\n",
      "     0.879    16.647\n",
      "-------iteration  5, old and new parameters--------\n",
      "     0.879    16.647\n",
      "     0.879    16.647\n",
      "\n",
      "\u001b[34m\u001b[1mGMM estimates (g'W*g, iteration over W):\u001b[22m\u001b[39m\n",
      "          coef         std  std ver. 2\n",
      "μ        0.879       0.217       0.217\n",
      "σ²      16.647       1.311       1.311\n",
      "\n"
     ]
    }
   ],
   "source": [
    "println(\"\\niterated GMM, using optimal weighting matrix, starting with S from previous estimation\")\n",
    "\n",
    "(par_4,par0) = (copy(par_1),copy(par_1))\n",
    "(Δpar,i)     = (Inf,1)\n",
    "\n",
    "println(\"\\n\\niterating over W starting with the W choice above\")\n",
    "while (Δpar > 1e-3) || (i < 2)    #require at least one iteration\n",
    "    #global Δpar, par_4, par0, i, W, S    #only needed in REPL/scripts\n",
    "    local Sol, g\n",
    "    println(\"-------iteration  $i, old and new parameters--------\")\n",
    "    W               = inv(S)\n",
    "    Sol             = optimize(par->Gmm4MomLossFn(par,x,W),par0)   #use last estimates as starting point\n",
    "    par_4           = Optim.minimizer(Sol)\n",
    "    printlnPs(par0')\n",
    "    printlnPs(par_4')\n",
    "    g,              = Gmm4MomFn(par_4,x)\n",
    "    S               = NWFn(g,1)\n",
    "    Δpar            = maximum(abs.(par_4-par0))\n",
    "    par0            = copy(par_4)             #par0=par_4 would make them always identical\n",
    "    i               = i + 1\n",
    " end\n",
    "\n",
    "V2 = inv(D'W*D)*D'W*S*W'D*inv(D'W*D)      #if non-optimal weighting matrix\n",
    "V1 = inv(D'inv(S)*D)                      #with optimal weighting matrix\n",
    "\n",
    "printblue(\"\\nGMM estimates (g'W*g, iteration over W):\")\n",
    "xx = [par_4 sqrt.(diag(V2/T)) sqrt.(diag(V1/T))]\n",
    "printTable(xx,[colNames;\"std ver. 2\"],rowNames,width=12)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 14,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\u001b[34m\u001b[1mW matrix used in the last iteration, (times 10000)\u001b[22m\u001b[39m\n",
      "  1525.564    39.433   -16.963    -0.674\n",
      "    39.433    18.778    -0.297    -0.050\n",
      "   -16.963    -0.297     0.306     0.012\n",
      "    -0.674    -0.050     0.012     0.001\n",
      "\n"
     ]
    }
   ],
   "source": [
    "printblue(\"W matrix used in the last iteration, (times 10000)\")\n",
    "\n",
    "printmat(W*10000)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "@webio": {
   "lastCommId": null,
   "lastKernelId": null
  },
  "anaconda-cloud": {},
  "kernelspec": {
   "display_name": "Julia 1.1.1",
   "language": "julia",
   "name": "julia-1.1"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.1.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 1
}
